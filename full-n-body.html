<!DOCTYPE html>
<html lang="en">
<head>
    <meta charset="UTF-8">
    <script type="module" src="full-n-body.js"></script>
    <title>N-Body Boids</title>
    <script type="x-shader/x-vertex" id="update-vs">
    #version 300 es

    in vec4 pos;

    void main() {
        gl_Position = pos;
    }
    </script>
    <script type="x-shader/x-fragment" id="boid-update-fs">
    #version 300 es
    precision highp float;

    uniform sampler2D posVelTex;
    uniform vec2 canvasDim;
    uniform int numParticles;
    uniform float dt;

    out vec4 color;

    ivec2 texDim;

    struct Particle {
        int i;
        vec2 pos;
        vec2 vel;
    };

    Particle getParticle(int i) {
        ivec2 texPos = ivec2(i % texDim.x, i / texDim.x);
        vec4 posVel = texelFetch(posVelTex, texPos, 0);
        return Particle(i, posVel.rg, posVel.ba);
    }

    void main() {
        texDim = textureSize(posVelTex, 0);
        int pIndex = int(gl_FragCoord.x) + texDim.x * int(gl_FragCoord.y);
        Particle p = getParticle(pIndex);

        float visualRange = 75.0;
        int neighbours = 0;

        float centeringFactor = 0.005;
        vec2 center = vec2(0);

        float avoidFactor = 0.1;
        float minDistance = 10.0;
        vec2 avoid = vec2(0);

        float matchingFactor = 0.05;
        vec2 avgVel = vec2(0);

        for (int i = 0; i < numParticles; i++) {
            Particle o;
            if (i == p.i) o = p; else o = getParticle(i);
            if (distance(p.pos, o.pos) < visualRange) {
                center += o.pos;
                avgVel += o.vel;
                neighbours++;
            }
            if (i != p.i && distance(p.pos, o.pos) < minDistance) {
                avoid += p.pos - o.pos;
            }
        }

        p.vel += (center/float(neighbours) - p.pos) * centeringFactor;
        p.vel += avoid * avoidFactor;
        p.vel += (1.0*avgVel/float(neighbours) - p.vel) * matchingFactor;

        float speedLimit = 15.0;
        float speed = sqrt(dot(p.vel, p.vel));
        if (speed > speedLimit) p.vel = (p.vel / speed) * speedLimit;

        float margin = 50.0;
        float turnFactor = 1.0;

        if (p.pos.x < margin) {
            p.vel.x += turnFactor;
        } else if (p.pos.x > canvasDim.x - margin) {
            p.vel.x -= turnFactor;
        }
        if (p.pos.y < margin) {
            p.vel.y += turnFactor;
        } else if (p.pos.y > canvasDim.y - margin) {
            p.vel.y -= turnFactor;
        }
        p.vel *= 1.05;

        p.pos += p.vel*dt*20.0;
        p.pos.x = max(p.pos.x, 0.0),
        p.pos.x = min(p.pos.x, canvasDim.x);
        color = vec4(p.pos, p.vel);
    }
    </script>
    <script type="x-shader/x-vertex" id="render-vs">
    #version 300 es

    uniform sampler2D posVelTex;
    uniform mat4 matrix;

    out vec4 iColor;

    vec4 getValue(sampler2D tex, ivec2 dim, int i) {
        int x = i % dim.x;
        int y = i / dim.x;
        return texelFetch(tex, ivec2(x, y), 0);
    }

    void main() {
        ivec2 texDim = textureSize(posVelTex, 0);
        vec4 posVel = getValue(posVelTex, texDim, gl_VertexID);
        vec2 pos = posVel.rg;
        vec2 vel = posVel.ba;

        gl_Position = matrix * vec4(pos.xy, 0, 1);
        gl_PointSize = 2.0;

        iColor = vec4(vel.x / 30.0 + 0.5, vel.y / 30.0 + 0.5, 1.0, 1.0);
    }
    </script>
    <script type="x-shader/x-fragment" id="render-fs">
    #version 300 es
    precision highp float;

    in vec4 iColor;

    out vec4 color;

    void main() {
        color = iColor;
    }
    </script>
</head>
<body style="background: black">
<canvas id="canvas" style="
    padding: 0;
    margin: auto;
    display: block;
    position: absolute;
    top: 0;
    bottom: 0;
    left: 0;
    right: 0;">
    Canvas not supported.
</canvas>
<div style="position: absolute">
    <p id="fps" style="font-family: Arial,sans-serif; color: white"></p>
    <label for="numParticlesSlider"></label>
    <input type="range" min="0" max="5" value="0" id="numParticlesSlider">
</div>
</body>
</html>
